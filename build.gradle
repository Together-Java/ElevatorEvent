buildscript {
    repositories {
        mavenCentral()
    }
}

def printOutput(def output) {
    return tasks.create("printOutput") {
        println "#educational_plugin_checker_version 1"
        def separator = System.lineSeparator()
        def lines = output.toString().split("(?<=${separator})|(?=${separator})")
        for (line in lines) {
            println "#educational_plugin" + line
        }
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'

    sourceCompatibility = 17

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.mockito:mockito-core:4.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    }

    sourceSets {
        main {
            java.srcDir 'src'
        }
        test {
            java.srcDir 'test'
        }
    }

    mainClassName = project.hasProperty("mainClass") ? project.getProperty("mainClass") : ""

    if (project.hasProperty("educationalRun") && "true".equalsIgnoreCase(project.getProperty("educationalRun"))) {
        def runOutput = new ByteArrayOutputStream()
        tasks.run.setStandardOutput(runOutput)
        tasks.run.doLast { printOutput(runOutput) }
    }

    test {
        useJUnitPlatform()
    }
}

project(':Contest-Assignment') {
    def log4jVersion = '2.18.0'
    def jbaVersion = '23.0.0'

    dependencies {
        annotationProcessor 'org.projectlombok:lombok:+'
        compileOnly 'org.projectlombok:lombok:+'

        annotationProcessor "org.jetbrains:annotations:$jbaVersion"
        compileOnly "org.jetbrains:annotations:$jbaVersion"

        implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
        implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    }

    sourceSets {
        main {
            resources.srcDir 'resources'
        }
    }
}

project(':util') {
    dependencies {
        testImplementation 'org.mockito:mockito-core:4.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    }

    test {
        useJUnitPlatform()
    }
}

configure(subprojects.findAll { it.name != 'util' }) {
    dependencies {
        implementation project(':util').sourceSets.main.output
        testImplementation project(':util').sourceSets.test.output
    }
}
